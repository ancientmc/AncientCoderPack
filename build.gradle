plugins {
    id 'java-library'
    id 'eclipse'
    id 'de.undercouch.download' version '4.1.2'
    id 'java-gradle-plugin'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

group = 'com.entropy.rcp'
version = '1.0-a1.2.6'
logger.lifecycle('Version: ' + version + ' Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

import com.entropy.rcp.OSName
import org.gradle.internal.os.OperatingSystem

repositories {
    mavenCentral()
    mavenLocal()
    maven { url = 'https://libraries.minecraft.net' }
    maven { url = 'https://nexus.velocitypowered.com/repository/maven-public/' }
    maven { url = 'https://www.beatunes.com/repo/maven2/' }
    maven { url = 'https://maven.fabricmc.net/' }
}

configurations {
    retroguard
    mcinjector
    enigma
    forgeflower
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:4.5'
    implementation 'org.ow2.asm:asm-all:4.1'
    implementation 'net.java.jinput:jinput:2.0.5'
    implementation 'net.java.jutils:jutils:1.0.0'
    if (OperatingSystem.current().isMacOsX()) {
        implementation 'org.lwjgl.lwjgl:lwjgl:2.9.1'
        implementation 'org.lwjgl.lwjgl:lwjgl_util:2.9.1'
    } else {
        implementation 'org.lwjgl.lwjgl:lwjgl:2.9.0'
        implementation 'org.lwjgl.lwjgl:lwjgl_util:2.9.0'
    }
    implementation 'com.paulscode:soundsystem:20120107'
    implementation 'com.paulscode:codecjorbis:20101023'
    implementation 'com.jcraft:jogg:0.0.7'
    implementation 'com.jcraft:jorbis:0.0.17'
    implementation 'com.paulscode:librarylwjglopenal:20100824'
    implementation 'com.paulscode:codecwav:20101023'

    retroguard 'de.oceanlabs.mcp:RetroGuard:3.6.6'
    retroguard configurations.retroguard.dependencies
    mcinjector 'de.oceanlabs.mcp:mcinjector:3.8.0'
    enigma 'cuchaz:enigma-cli:2.1.0'
    enigma 'cuchaz:enigma:2.1.0'
    enigma configurations.enigma.dependencies
    forgeflower 'net.minecraftforge:forgeflower:1.5.605.9'
}

ext {
    OSNAME = OSName.getOSName()
    MC_VERSION = "a1.2.6"
    MC_JAR = "https://piston-data.mojang.com/v1/objects/a68c817afd6c05c253ba5462287c2c19bbb57935/client.jar"
    RCP_DIR_CORE = "$project.projectDir\\core"
    RCP_DIR_MAPPING = "${RCP_DIR_CORE}\\mapping"
    RCP_DIR_LOGS = "${RCP_DIR_CORE}\\logs"
    RCP_DIR_TEMP = "${RCP_DIR_CORE}\\temp"
    RCP_DIR_PATCHES = "${RCP_DIR_CORE}\\patches"
    RCP_PATCH_FILE = "${RCP_DIR_PATCHES}\\patch\\minecraft.patch"
    RCP_CODECMUS_PATCH_FILE = "${RCP_DIR_PATCHES}\\patch\\CodecMus.patch"
    RCP_DIR_JARS = "$project.projectDir\\jars\\"
    RCP_DIR_NATIVES = "${RCP_DIR_JARS}\\bin\\natives\\${OSNAME}\\"
    RCP_DIR_SRC = "src\\main\\java"
    RCP_DIR_RESOURCES = "src\\main\\resources"

    RCP_RG = project.getConfigurations().findByName("retroguard")
    RCP_MCINJECTOR = project.getConfigurations().findByName("mcinjector")
    RCP_ENIGMA = project.getConfigurations().findByName("enigma")
    RCP_FORGEFLOWER = project.getConfigurations().findByName("forgeflower")
    RCP_ASSET_EXTRACTOR = "${RCP_DIR_PATCHES}\\McAssetExtractor-1.0_02-w-dependencies.jar"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.withType(JavaExec) {
    systemProperty "java.library.path", "${RCP_DIR_NATIVES}"
}

/**
 * WARNING: This task will delete all main RCP-generated files within the directory, including MC sources. This is intended for
 * if you wanted to reset the main build process for whatever reason, or if something has gone horribly wrong.
 */
task cleanAndStartOver(type: Delete) {
    delete (file(RCP_DIR_TEMP).listFiles(), // temporary files
            file(RCP_DIR_SRC + "\\net"), // Minecraft sources
            file(RCP_DIR_RESOURCES).listFiles(), // Everything in the main resources folder
            file(RCP_DIR_JARS + "resources"), // The runtime resources folder
            file(RCP_DIR_NATIVES).listFiles(), // LWJGL and JInput natives
            file(RCP_DIR_JARS + "saves"), // MC saves folder
            file(RCP_DIR_JARS + "texturepacks"), // MC texturepacks folder
            file(RCP_DIR_JARS + "options.txt")) // MC options text file
            file(RCP_DIR_LOGS).listFiles() // logs
}


task downloadJar(type: Download) {
    src MC_JAR
    dest file("${RCP_DIR_TEMP}\\${MC_VERSION}.jar")
}

task jarDeobf(type: JavaExec, dependsOn: downloadJar) {
    inputs.file downloadJar.dest
    main = "RetroGuard"
    classpath = files(RCP_RG)
    args ("-searge", "${RCP_DIR_MAPPING}\\retroguard.cfg")
}

// We have to inject the exceptions first before applying parameters because otherwise MCInjector will override
// our parameters with placeholder SRG names.
task injectExceptions(type: JavaExec) {
    main = "de.oceanlabs.mcp.mcinjector.MCInjector"
    classpath = files(RCP_MCINJECTOR)
    args ("--in", "${RCP_DIR_TEMP}\\${MC_VERSION}-srg.jar", "--out", "${RCP_DIR_TEMP}\\${MC_VERSION}-exc.jar", "--exc", "${RCP_DIR_MAPPING}\\exceptions.exc", "--log", "${RCP_DIR_LOGS}\\exceptions.log")
}

task addParams(type: JavaExec) {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    main = "cuchaz.enigma.command.Main"
    classpath = files(RCP_ENIGMA)
    args ("deobfuscate", "${RCP_DIR_TEMP}\\${MC_VERSION}-exc.jar", "${RCP_DIR_TEMP}\\${MC_VERSION}-temp.jar", "${RCP_DIR_MAPPING}\\params")
}

// Forgeflower throws out an error when decompiling these files and decompiling the net.minecraft.client files, but it's
// not a big deal since it doesn't interrupt the decompilation process, and any weird artifacts can just get patched.
task decompileClassFiles(type: JavaExec) {
    main = "org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler"
    classpath = files(RCP_FORGEFLOWER)
    args('-rbr=1', '-asc=0', '-dgs=1', '-jvn=1', '-ind="\t"', "${RCP_DIR_TEMP}\\${MC_VERSION}-temp.jar", "${RCP_DIR_TEMP}\\${MC_VERSION}-final.jar")
}

task unzipJar(type: Copy) {
    from zipTree(file("${RCP_DIR_TEMP}\\${MC_VERSION}-final.jar"))
    into file(RCP_DIR_SRC)
    exclude ('com/**', 'paulscode/**')
}

// TODO: Re-write patch files and use external DiffPatch plugin.
task patchSourceFiles(type: Exec) {
    commandLine("${RCP_DIR_PATCHES}\\applydiff.exe", '-p', '1', '-u', '-i', RCP_PATCH_FILE, '-d', RCP_DIR_SRC)
}

task patchCodecMusFiles(type: Exec) {
    commandLine("${RCP_DIR_PATCHES}\\applydiff.exe", '-p', '1', '-u', '-i', RCP_CODECMUS_PATCH_FILE, '-d', RCP_DIR_SRC)
}

task copyJarAssets(type: Copy) {
    group = "RCP"
    description = "Copies the assets found in the JAR file."
    from zipTree(file("${RCP_DIR_TEMP}\\${MC_VERSION}.jar"))
    into file(RCP_DIR_RESOURCES)
    exclude('com/**', 'net/**', 'paulscode/**', '*.class')
}

/**
 * Modification of GitHub user rhmeuer's program McAssetExtractor.
 */
task downloadMetaAssets(type: JavaExec) {
    group = "RCP"
    description = "downloads the meta assets (mainly sound files) from Mojang's servers."
    main = "com.github.rmheuer.mcasset.McAssetExtractor"
    classpath = files(RCP_ASSET_EXTRACTOR)
    args(MC_VERSION, file(RCP_DIR_JARS))
}

// There's definitely a better way to do this.
task downloadNatives {
    download {
        if(OperatingSystem.current().isMacOsX()) {
            src ("https://repo.maven.apache.org/maven2/org/lwjgl/lwjgl/lwjgl-platform/2.9.0/lwjgl-platform-2.9.1-natives-${OSNAME}.jar")
            dest "${RCP_DIR_NATIVES}"
        } else {
            src ("https://repo.maven.apache.org/maven2/org/lwjgl/lwjgl/lwjgl-platform/2.9.0/lwjgl-platform-2.9.0-natives-${OSNAME}.jar")
            dest "${RCP_DIR_NATIVES}"
        }
    }
    download {
        src ("https://repo.maven.apache.org/maven2/net/java/jinput/jinput-platform/2.0.5/jinput-platform-2.0.5-natives-${OSNAME}.jar")
        dest "${RCP_DIR_NATIVES}"
    }
}

// Again, there's probably a better way to do this.
task extractNatives {
    copy {
        if(OperatingSystem.current().isMacOsX()) {
            from(zipTree(file("${RCP_DIR_NATIVES}\\lwjgl-platform-2.9.1-natives-osx.jar")))
            into(file("${RCP_DIR_NATIVES}"))
            exclude('META-INF/**')
        } else {
            from(zipTree(file("${RCP_DIR_NATIVES}\\lwjgl-platform-2.9.0-natives-${OSNAME}.jar")))
            into(file("${RCP_DIR_NATIVES}"))
            exclude('META-INF/**')
        }
    }
    copy {
        from(zipTree(file("${RCP_DIR_NATIVES}\\jinput-platform-2.0.5-natives-${OSNAME}.jar")))
        into(file("${RCP_DIR_NATIVES}"))
        exclude('META-INF/**')
    }
}

task cleanUpRemnants (type: Delete) {
    delete (file(RCP_DIR_TEMP).listFiles(),
            file("${RCP_DIR_NATIVES}\\jinput-platform-2.0.5-natives-${OSNAME}.jar"),
            file("${RCP_DIR_NATIVES}\\lwjgl-platform-2.9.0-natives-${OSNAME}.jar"),
            file("${RCP_DIR_NATIVES}\\lwjgl-platform-2.9.1-natives-${OSNAME}.jar"))
}

task runClient(type: JavaExec) {
    description = "Runs the client."
    main = "com.entropy.rcp.Start"
    classpath = sourceSets.main.runtimeClasspath
}